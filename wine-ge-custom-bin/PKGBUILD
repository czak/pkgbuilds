pkgname=wine-ge-custom-bin
_srctag=GE-Proton8-26
pkgver=${_srctag//-/.}
pkgrel=1
pkgdesc="A compatibility layer for running Windows programs - GloriousEggroll binary build"
arch=(x86_64)
url="https://github.com/GloriousEggroll/wine-ge-custom"
license=(LGPL)
provides=("wine=8.0" "wine-wow64=8.0")
conflicts=('wine' 'wine-wow64')

source=("https://github.com/GloriousEggroll/wine-ge-custom/releases/download/${_srctag}/wine-lutris-${_srctag}-x86_64.tar.xz")
sha256sums=('9cef292850a470b1a2637b22959051ab36784dccdb4966c19607790a1e3a96f1')

depends=(
  attr             lib32-attr
  fontconfig       lib32-fontconfig
  libxcursor       lib32-libxcursor
  libxrandr        lib32-libxrandr
  libxi            lib32-libxi
  gettext          lib32-gettext
  freetype2        lib32-freetype2
  gcc-libs         lib32-gcc-libs
  libpcap          lib32-libpcap
  desktop-file-utils
)

optdepends=(
  giflib                lib32-giflib
  gnutls                lib32-gnutls
  v4l-utils             lib32-v4l-utils
  libpulse              lib32-libpulse
  alsa-plugins          lib32-alsa-plugins
  alsa-lib              lib32-alsa-lib
  libxcomposite         lib32-libxcomposite
  libxinerama           lib32-libxinerama
  opencl-icd-loader     lib32-opencl-icd-loader
  libva                 lib32-libva
  gtk3                  lib32-gtk3
  gst-plugins-base-libs lib32-gst-plugins-base-libs
  gst-plugins-good      lib32-gst-plugins-good
  vulkan-icd-loader     lib32-vulkan-icd-loader
  sdl2                  lib32-sdl2
  libgphoto2
  ffmpeg
  dosbox
)

package() {
  _output="${srcdir}/lutris-${_srctag}-x86_64"

  install -d -m755 ${pkgdir}/opt/${pkgname}
  install -d -m755 ${pkgdir}/usr/bin

  cp -r ${_output}/* ${pkgdir}/opt/${pkgname}

  BINARIES="wine wine64 wineboot wineserver"
  for binary in ${BINARIES}; do
    ln -s /opt/${pkgname}/bin/${binary} ${pkgdir}/usr/bin/${binary}
  done
}
